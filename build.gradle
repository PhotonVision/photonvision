plugins {
    id "com.diffplug.spotless" version "6.1.2"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.github.node-gradle.node" version "3.1.1" apply false
    id "edu.wpi.first.GradleJni" version "1.0.0"
    id "edu.wpi.first.GradleVsCode" version "1.1.0"
    id "edu.wpi.first.NativeUtils" version "2022.8.1" apply false
    id "edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin" version "2020.2"
    id "org.hidetake.ssh" version "2.10.1"
}

allprojects {
    repositories {
        mavenCentral()
        maven { url = "https://maven.photonvision.org/repository/internal/" }
    }
    wpilibRepositories.addAllReleaseRepositories(it)
    wpilibRepositories.addAllDevelopmentRepositories(it)
}

// Configure the version number.
apply from: "versioningHelper.gradle"

ext {
    wpilibVersion = "2022.1.1"
    opencvVersion = "4.5.2-1"
    joglVersion = "2.4.0-rc-20200307"
    pubVersion = versionString
    isDev = pubVersion.startsWith("dev")

    if(project.hasProperty('pionly')) {
        jniPlatforms = ['linuxraspbian']
    } else if(project.hasProperty('winonly')) {
        jniPlatforms = ['windowsx86-64']
    } else {
        jniPlatforms = ['linuxaarch64bionic', 'linuxraspbian', 'linuxx86-64', 'osxx86-64', 'windowsx86-64']

    }

    println("Building for archs " + jniPlatforms)
}

spotless {
    java {
        toggleOffOn()
        googleJavaFormat()
        indentWithTabs(2)
        indentWithSpaces(4)
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        target "**/*.java"
        targetExclude("photon-core/src/main/java/org/photonvision/PhotonVersion.java")
        targetExclude("photon-lib/src/main/java/org/photonvision/PhotonVersion.java")
    }
}
