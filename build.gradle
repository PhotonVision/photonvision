plugins {
    id "com.diffplug.gradle.spotless" version "3.28.0"
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "com.github.node-gradle.node" version "2.2.4" apply false
    id "edu.wpi.first.GradleJni" version "1.0.0"
    id "edu.wpi.first.GradleVsCode" version "1.0.0"
    id "edu.wpi.first.NativeUtils" version "2022.4.4" apply false
    id "edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin" version "2020.2"
    id "org.hidetake.ssh" version "2.10.1"
}

allprojects {
    repositories {
        jcenter()
        maven { url = "https://maven.photonvision.org/repository/internal/" }
    }
    wpilibRepositories.addAllReleaseRepositories(it)
    wpilibRepositories.addAllDevelopmentRepositories(it)
}

// Configure the version number.
apply from: "versioningHelper.gradle"

ext {
    wpilibVersion = "2022.1.1"
    opencvVersion = "4.5.2-1"
    joglVersion = "2.4.0-rc-20200307"
    pubVersion = versionString
    isDev = pubVersion.startsWith("dev")


    jniPlatforms = project.hasProperty('pionly') ? ['linuxraspbian']
        : ['linuxaarch64bionic', 'linuxraspbian', 'linuxx86-64', 'osxx86-64', 'windowsx86-64']

    println("Building for archs " + jniPlatforms)
}

spotless {
    java {
        googleJavaFormat()
        paddedCell()
        indentWithTabs(2)
        indentWithSpaces(4)
        removeUnusedImports()
    }
    java {
        target "**/*.java"
        licenseHeaderFile "$rootDir/LicenseHeader.txt"
        targetExclude("photon-core/src/main/java/org/photonvision/PhotonVersion.java")
        targetExclude("photon-lib/src/main/java/org/photonvision/PhotonVersion.java")
    }
}
