plugins {
    id "com.diffplug.spotless" version "6.1.2"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "com.github.node-gradle.node" version "3.1.1" apply false
    id "edu.wpi.first.GradleJni" version "1.0.0"
    id "edu.wpi.first.GradleVsCode" version "1.1.0"
    id "edu.wpi.first.NativeUtils" version "2023.10.0" apply false
    id "edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin" version "2020.2"
    id "org.hidetake.ssh" version "2.10.1"
    id 'edu.wpi.first.WpilibTools' version '1.0.0'
}

import org.gradle.api.internal.artifacts.dependencies.DefaultExternalModuleDependency;

allprojects {
    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = "https://maven.photonvision.org/repository/internal/" }
    }
    wpilibRepositories.addAllReleaseRepositories(it)
    wpilibRepositories.addAllDevelopmentRepositories(it)
}

// Configure the version number.
apply from: "versioningHelper.gradle"

ext {
    wpilibVersion = "2023.1.1-beta-7-15-g1e7fcd5"
    opencvVersion = "4.6.0-4"
    joglVersion = "2.4.0-rc-20200307"
    pubVersion = versionString
    isDev = pubVersion.startsWith("dev")

    if(project.hasProperty('pionly')) {
        jniPlatforms = ['linuxarm32']
    } else if(project.hasProperty('winonly')) {
        jniPlatforms = ['windowsx86-64']
    } else if(project.hasProperty('aarch64only')) {
        jniPlatforms = ['linuxaarch64bionic']
    } else {
        jniPlatforms = ['linuxarm64', 'linuxarm32', 'linuxx86-64', 'osxuniversal', 'windowsx86-64']
    }

    println("Building for archs " + jniPlatforms)

}

wpilibTools.deps.wpilibVersion = wpilibVersion

spotless {
    java {
        toggleOffOn()
        googleJavaFormat()
        indentWithTabs(2)
        indentWithSpaces(4)
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
    }
    java {
        target "**/*.java"
        targetExclude("photon-core/src/main/java/org/photonvision/PhotonVersion.java")
        targetExclude("photon-lib/src/main/java/org/photonvision/PhotonVersion.java")
    }
}
